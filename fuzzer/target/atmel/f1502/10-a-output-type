#!/usr/bin/perl -Ilib
#
# Fuzzer script to detect Atmel ATF1502 Output Type configuration
#
# Copyright (c) 2025 Alexei A. Smekalkine <ikle@ikle.ru>
#
# SPDX-License-Identifier: BSD-2-Clause
#

use strict;
use warnings;

use Atmel::F1500::Fuzzer;
use Atmel::F1500::MCC;
use Atmel::F1502;

sub make_sample_0 ($$$) {
	my ($o, $pos, $neg) = @_;
	my $path = $o->{path};
	my $dev  = $o->{dev};
	my $cols = $o->{cols};
	my $rows = $o->{rows};
	my $lab  = $o->{lab};

	my $test = make_base ($path, $o->{head});

	for my $i (@{$pos}) {
		my $n = 1 + $i;		# LAB A only at a moment

		print $test "P$n = data;\n";
	}

	for my $i (@{$neg}) {
		my $n = 1 + $i;		# LAB A only at a moment

		print $test "P$n.d  = data;\n";
		print $test "P$n.ck = GCK2;\n";
	}

	return undef unless compile ($path, $dev, '-strategy', 'Optimize', 'off');

	f1502_update ($o->{conf}, $path);

	return mcc_read_conf ($cols, $rows, $path, $lab);
}

sub make_sample_1 ($$$) {
	my ($o, $pos, $neg) = @_;
	my $path = $o->{path};
	my $dev  = $o->{dev};
	my $cols = $o->{cols};
	my $rows = $o->{rows};
	my $lab  = $o->{lab};

	my $test = make_base ($path, $o->{head});

	print $test "[P17..P32].d  = [F1..F16];\n";
	print $test "[P17..P32].ck = GCK2;\n";

	for my $i (@{$pos}) {
		my $n = 1 + $i;		# LAB A only at a moment

		print $test "F$n.d  = data;\n";
		print $test "F$n.ck = GCK2;\n";
		print $test "P$n    = data;\n";
	}

	for my $i (@{$neg}) {
		my $n = 1 + $i;		# LAB A only at a moment

		print $test "F$n.d  = data;\n";
		print $test "F$n.ck = GCK2;\n";
		print $test "P$n    = F$n;\n";
	}

	return undef unless compile ($path, $dev, '-strategy', 'Optimize', 'off');

	f1502_update ($o->{conf}, $path);

	return mcc_read_conf ($cols, $rows, $path, $lab);
}

my $o = f1502_load ('db');

#
# OF -- Functional Output (not registered)
#
# PT4 = 0	-- PT4 inactive
# PCK = OF
# CE  = OF
#
# PTCK = PCK ? PT4 : 1 = PCK ? 0 : 1 = !PCK = !OF
#
# ck = CE ? PTCK : GCK2 = OF ? !OF : GCK2 = OF ? 0 : GCK2 = !OF & GCK2
# ce = CE | PTCK = OF | !OF = 1
#
# Bit at column 4, middle
# Bit at column 9, outer	-- same as for test #1
#
my %conf_0 = (
	'cb'	=> \&make_sample_0,	# sample table generator	(req)
	'name'	=> 'OF_',		# prefix name of source bits	(req)
	'cols'	=> 12,			# output table column count	(req)
	'rows'	=> 32,			# output table row count	(req)
	'count'	=> 16,			# number of source bits		(req)
	'order'	=> 5,			# ceil (log2 (count - 1)) + 1	(req)

	'path'	=> 'work/test',		# test files prefix
	'head'	=> "$0-base.pld",	# PLD file base part
	'dev'	=> 'P1502C44',		# target device for fitter
	'lab'	=> 'A',			# LAB name to test
	'conf'	=> $o,			# configuration database
);

my $mcc_0 = make_bit_map (\%conf_0);

#
# OF -- Functional Output (not registered)
#
# Bit at column 8, middle
# Bit at column 9, outer	-- same as for test #0
#
my %conf_1 = (
	'cb'	=> \&make_sample_1,	# sample table generator	(req)
	'name'	=> 'OF_',		# prefix name of source bits	(req)
	'cols'	=> 12,			# output table column count	(req)
	'rows'	=> 32,			# output table row count	(req)
	'count'	=> 16,			# number of source bits		(req)
	'order'	=> 5,			# ceil (log2 (count - 1)) + 1	(req)

	'path'	=> 'work/test',		# test files prefix
	'head'	=> "$0-base.pld",	# PLD file base part
	'dev'	=> 'P1502C44',		# target device for fitter
	'lab'	=> 'A',			# LAB name to test
	'conf'	=> $o,			# configuration database
);

my $mcc_1 = make_bit_map (\%conf_1);

#
# Final computation
#

die "E: Fuzzing results absent\n" unless defined $mcc_0 and defined $mcc_1;

my $mcc = mcc_intersect ($mcc_0, $mcc_1);

mcc_update ($o->{'mcc'}, $mcc) if defined $mcc;

f1502_report ($o);
f1502_save   ($o, 'db');

